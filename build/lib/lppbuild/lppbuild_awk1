#******************************************************************************
#                                                                             *
# LPPBUILD_AWK1                                                               *
#                                                                             *
# This awk script is used to process the list of files which make up a        *
# component of an LPP and decide from their names what type of LPP we are     *
# dealing with.  The script is passed the complete list of the names of all   *
# the files which are to be included in the component and will count them in  *
# categories according to what kind of fileset they imply.                    *
#                                                                             *
# The decision logic for this is as follows:                                  *
#                                                                             *
# The LPP is a share part only if all files within it are targetted at the    *
# /usr/share directory or any subdirectory of it.                             *
#                                                                             *
# The LPP is user part only if all the target files are within /usr. Any files*
# outside this (/etc, /dev, /var, /sbin, etc) belong to the root part and will*
# force the package to be a root+user type. It is not allowed to have a root  *
# only package.                                                               *
#                                                                             *
# Share parts must not be mixed with root/user parts.                         *
#                                                                             *
# The installp code for the LPP type is return via stdout as follows:         *
#                                                                             *
# U - user part only                                                          *
# B - Both root and user parts                                                *
# H - Share part only                                                         *
#                                                                             *
#******************************************************************************
#******************************************************************************
#                                                                             *
# Begin procedure. Set up the field delimiter and initialize counters used    *
# to track the number of each type of file name.                              *
#                                                                             *
#******************************************************************************

BEGIN { FS="/"                         # will be examining file names
 shares=0                              # clear accumulator
 users=0                               # clear accumulator
 roots=0                               # clear accumulator
}

#******************************************************************************
#                                                                             *
# Main processing loop. We are passed records which contain file names of the *
# form "./aa/bb/cc" etc, etc and our delimiter is "/". This means that $2 is  *
# the first part of the target file name and $3 is its first subdirectory.    *
# Test to see if we have a file in /usr/share (which immediately implies a    *
# share part) and then test for files in /usr (which implies a user part). If *
# the file is outside these two, then we must assume that it is a root part.  *
#                                                                             *
#******************************************************************************

{
 if ($2=="usr" && $3=="share")         # is this in /usr/share
  {                                    # looks like it
   shares+=1                           # so record this as a hit
   next                                # and go round again
  }                                    # end of share test
 if ($2=="usr")                        # otherwise is it in the user part
  {                                    # looks like it
   users+=1                            # so record this as a hit
   next                                # and go round again
  }                                    # end of root test
 roots+=1                              # must be neither of above then
}

#******************************************************************************
#                                                                             *
# END processing. See what we have garnered about the number of each type     *
# of file and decide from this what type of LPP we are dealing with here.     *
#                                                                             *
#******************************************************************************

END {
 allfiles=users+roots+shares;          # how many files did we find?
 if (allfiles == 0)                    # just to be on the safe side
  {                                    # forgot to give us any data
   print "?"                           # this is not going to work
   exit;                               # so get out of here
  }                                    # end of error handling
 if (users==0 && roots==0)             # only met /usr/share files?
   print "H"                           # yes, signal share part
 else                                  # otherwise we had more
   if (roots==0)                       # user or root and user?
     print "U"                         # user only, no root files here
   else                                # otherwise we had both
     print "B"                         # so signal that
}

